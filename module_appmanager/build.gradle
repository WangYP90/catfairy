if (isAppManagerRunAlone.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'com.jakewharton.butterknife'
android {
    compileSdkVersion compile_Sdk_Version.toInteger()
    defaultConfig   {
        if (isAppManagerRunAlone.toBoolean()) {
            applicationId "com.tj24.appmanager"
            versionName "1.0"
            versionCode 1
        }
        minSdkVersion min_Sdk_Version.toInteger()
        targetSdkVersion target_Sdk_Version.toInteger()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME : project.getName()]
            }
        }
        resourcePrefix "app"
        multiDexEnabled true
    }

    // 指定jdk版本
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            if (!isAppManagerRunAlone.toBoolean()) {
                manifest.srcFile 'src/main/lib_Manifest/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    exclude 'debug/**'
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation project(':library_base')
    //ARouter
    annotationProcessor "com.alibaba:arouter-compiler:${arouter_compiler_Version}"
    //butterknife
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterknife_Version}"
}
